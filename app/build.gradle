plugins {
	id 'com.android.application'
	id 'kotlin-android'
	id 'kotlin-android-extensions'
	id 'kotlin-kapt'
}

def gitCommits = 'git rev-list --count HEAD'.execute([], rootDir).text.trim().toInteger()

android {
	compileSdkVersion 29
	buildToolsVersion '29.0.3'

	defaultConfig {
		applicationId 'org.koitharu.kotatsu'
		minSdkVersion 21
		targetSdkVersion 29
		versionCode gitCommits
		versionName '0.5-rc3'

		kapt {
			arguments {
				arg 'room.schemaLocation', "$projectDir/schemas".toString()
			}
		}
	}
	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}
	kotlinOptions {
		jvmTarget = JavaVersion.VERSION_1_8.toString()
		freeCompilerArgs += "-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi"
	}
	buildTypes {
		debug {
			applicationIdSuffix = '.debug'
		}
		release {
			multiDexEnabled false
			minifyEnabled true
			shrinkResources true
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
		}
	}
	lintOptions {
		disable 'MissingTranslation'
		abortOnError false
	}
	testOptions {
		unitTests.includeAndroidResources = true
		unitTests.returnDefaultValues = true
	}
}
androidExtensions {
	experimental = true
}
dependencies {
	implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
	implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
	implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.7'
	implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.7'

	implementation 'androidx.core:core-ktx:1.5.0-alpha01'
	implementation 'androidx.activity:activity-ktx:1.2.0-alpha06'
	implementation 'androidx.fragment:fragment-ktx:1.3.0-alpha06'
	implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.3.0-alpha05'
	implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta7'
	implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
	implementation 'androidx.recyclerview:recyclerview:1.2.0-alpha04'
	implementation 'androidx.viewpager2:viewpager2:1.1.0-alpha01'
	implementation 'androidx.preference:preference-ktx:1.1.1'
	implementation 'androidx.work:work-runtime-ktx:2.4.0-rc01'
	implementation 'com.google.android.material:material:1.3.0-alpha01'
	//noinspection LifecycleAnnotationProcessorWithJava8
	kapt 'androidx.lifecycle:lifecycle-compiler:2.3.0-alpha05'

	implementation 'androidx.room:room-runtime:2.2.5'
	implementation 'androidx.room:room-ktx:2.2.5'
	kapt 'androidx.room:room-compiler:2.2.5'

	implementation 'com.github.moxy-community:moxy:2.1.2'
	implementation 'com.github.moxy-community:moxy-androidx:2.1.2'
	implementation 'com.github.moxy-community:moxy-material:2.1.2'
	implementation 'com.github.moxy-community:moxy-ktx:2.1.2'
	kapt 'com.github.moxy-community:moxy-compiler:2.1.2'

	implementation 'com.squareup.okhttp3:okhttp:4.7.2'
	implementation 'com.squareup.okio:okio:2.6.0'
	implementation 'org.jsoup:jsoup:1.13.1'

	implementation 'org.koin:koin-android:2.1.5'
	implementation 'io.coil-kt:coil:0.11.0'
	implementation 'com.davemorrissey.labs:subsampling-scale-image-view:3.10.0'
	implementation 'com.tomclaw.cache:cache:1.0'

	debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.3'
	debugImplementation 'com.github.ChuckerTeam.Chucker:library:3.2.0'
	releaseImplementation 'com.github.ChuckerTeam.Chucker:library-no-op:3.2.0'

	testImplementation 'junit:junit:4.13'
	testImplementation 'org.json:json:20200518'
}